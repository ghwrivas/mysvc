plugins {
	id 'org.springframework.boot' version '2.4.0'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'java'
	id "org.flywaydb.flyway" version "7.3.1"
}

group = 'msvcdojo'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-rest'
	runtimeOnly 'mysql:mysql-connector-java'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web', {
		exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
	}
	implementation 'org.springframework.boot:spring-boot-starter-jetty', {
		exclude group: 'org.eclipse.jetty.websocket'
	}

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

flyway {
	url = 'jdbc:mysql://localhost:3306/demo'
	user = 'demo_user'
	password = 'demo_pass'
}

test {

	useJUnitPlatform()
	testLogging {
		afterSuite { desc, result ->

			if (!desc.parent) {
				println "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
			}
		}
	}
	reports {
		junitXml.enabled = false
		html.enabled = true
	}
}
